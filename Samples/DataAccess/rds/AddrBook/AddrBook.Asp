<HTML>
<HEAD>
<TITLE>Corporate Address Book</TITLE>
</HEAD>

<!--
	Purpose:	To provide a company directory search service for Web users.
	Written By:	Microsoft Data Access Group, Microsoft Corp.
	Date:		May, 1997 (Last update: April, 1999)
	© 1991-1999 Microsoft Corporation. All rights reserved.
-->

<BODY BACKGROUND="Arcadia.gif" LANGUAGE="VBScript" >
<TABLE BORDER=3 CELLPADDING=8 WIDTH="100%">
	<TR>
		<TH>
			<H1><font color="#160B5A">Arcadia Bay Corporate Phone Directory</font></H1>
		</TH>
	</TR>
</TABLE>

<h2><font color = "#160B5A">Search Parameters</h2>
<h5><font color = "#160B5A">Please enter one or more search patterns and press FIND to search.</h5>

<PRE> First Name     <INPUT NAME=SFirst SIZE=30> </PRE>

<PRE> Last Name      <INPUT NAME=SLast  SIZE=30> </PRE>

<PRE> Title          <INPUT NAME=STitle SIZE=30> </PRE>

<PRE> E-mail Alias   <INPUT NAME=SEmail SIZE=30> </PRE>

<!-- 
	DataSource Command button options:

	Find		Submit a search request to the database.
	Clear		Clear the QBE fields (labor saving function only).
	Update Profile	Send updated "address profile" back to the database.
	Cancel Changes	Undo all changes since the last Update Profile.
-->

<INPUT TYPE=BUTTON NAME="Find" 		VALUE="Find">
<INPUT TYPE=BUTTON NAME="Clear" 	VALUE="Clear">
<INPUT TYPE=BUTTON NAME="Update" 	VALUE="Update Profile">
<INPUT TYPE=BUTTON NAME="Cancel" 	VALUE="Cancel Changes">

<h2><font color = "#160B5A">Search Results</h2>

<TABLE DATASRC=#DC1>
<!--
	'ColumnHeads' row is dynamically generated by calling BuildColHeads() 
	in the Window_OnLoad sub.
-->
	<THEAD>
	<TR ID="ColumnHeads"></TR>
	</THEAD>

	<TR onClick="setPosition(window.event.srcElement.recordNumber)" ID="CellRows">
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=20 DATAFLD=FirstName id=TEXT1 name=TEXT1></TD>
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=20 DATAFLD=LastName id=TEXT2 name=TEXT2></TD>
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=20 DATAFLD=Title id=TEXT3 name=TEXT3></TD>
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=15 DATAFLD=Email id=TEXT4 name=TEXT4></TD>
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=5 DATAFLD=Building id=TEXT5 name=TEXT5></TD>
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=5 DATAFLD=Room id=TEXT6 name=TEXT6></TD>
		<TD><INPUT TYPE="TEXT" DATASRC=#DC1 SIZE=10 DATAFLD=Phone id=TEXT7 name=TEXT7></TD>
	</TR>
</TABLE>


<!-- 
	Recordset command button options:

	First			Move to the first record in the recordset.
	Previous		Move to the previous record.
	Next			Move to the next record.
	Last			Move to the last record in the recordset.
-->
<INPUT TYPE=BUTTON NAME="First" VALUE="First">
<INPUT TYPE=BUTTON NAME="Prev"  VALUE="Previous">
<INPUT TYPE=BUTTON NAME="Next"  VALUE="Next">
<INPUT TYPE=BUTTON NAME="Last"  VALUE="Last">


<!-- Non-visual control: RDS.DataControl -->
<OBJECT 
	classid="clsid:BD96C556-65A3-11D0-983A-00C04FC29E33"
	ID=DC1
	WIDTH=1 
	HEIGHT=1>
	<PARAM NAME="SERVER" VALUE="http://<%=Request.ServerVariables("SERVER_NAME")%>">
	<PARAM NAME="CONNECT" VALUE="Remote Provider=sqloledb;Initial Catalog=AddrBookDb;User ID=adcdemo;Password=adcdemo;">
</OBJECT>



<!-- VBS scripting for composing queries, updating profiles, and retrieving search results. -->

<SCRIPT LANGUAGE="VBScript">
'---- enum Values ----
Const adcExecSync = 1
Const adcExecAsync = 2

'---- enum Values ----
Const adcFetchUpFront = 1
Const adcFetchBackground = 2
Const adcFetchAsync = 3

Dim myQuery


SUB Window_OnLoad

	'Change the execution options such that execution is synchronous
	'and Fetching can occur in the background.
	DC1.ExecuteOptions = adcExecSync
	DC1.FetchOptions = adcFetchBackground
	
	'Initialize data grid with column names only.
	DC1.SQL = "Select FirstName, LastName, Title, Email, Building, Room, Phone from Employee where 2 < 1"
	DC1.Refresh
	
	Call BuildColHeads()

END SUB


'Dynamically generates column heads from recordset field names
Sub BuildColHeads()
	
	For i=0 to DC1.RecordSet.Fields.count-1
		Set oNewNode = document.createElement("TH")
		ColumnHeads.appendChild(oNewNode)	
		oNewNode.InnerText = DC1.Recordset.Fields(i).name 
	Next
	
End Sub


'Applies formatting to HTML table to visually indicate current record with
'blue border. Called by TR onClick in HTML table, above, as well as onClick
'event handlers for navigation buttons.
Sub SetPosition(iPosition)

	'set absolute position in recordset
	DC1.Recordset.AbsolutePosition = iPosition
	
	'clear row borders & highlight the current row in the bound table
	For i = 0 to document.all("CellRows").length-1
		document.all("CellRows").item(i).style.backgroundColor = ""
	Next
	
	document.all("CellRows").item(iPosition-1).style.backgroundColor = "blue"
	
End Sub


'Implement "Clear" button - clears all of the QBE fields in preparation for a new "Find."
SUB Clear_OnClick
	SFirst.Value=""
	SLast.Value=""
	STitle.Value=""
	SEmail.Value=""
END SUB


'Implement "Find" button - composes a dynamic SQL query to be processed by the database and returns matching records to be bound to the SGrid object.
SUB Find_OnClick
	Dim myQuery
	Dim Filtered

	myQuery = "Select FirstName, LastName, Title, Type, Email, ManagerEmail, " & _
	          "Building, Room, Phone from Employee"
	          
	Filtered = False
	
	'Check QBE fields and compose a dynamic SQL query.
	IF (SFirst.Value <> "") THEN
		myQuery = myQuery + " where FirstName like '" + SFirst.Value + "%'"
		Filtered = True
	END IF

	IF (SLast.Value <> "") THEN
		IF Filtered THEN
			myQuery = myQuery + " and LastName like '" + SLast.Value + "%'"
		ELSE
			myQuery = myQuery + " where LastName like '" + SLast.Value + "%'"
			Filtered = True
		END IF
	END IF

	IF (STitle.Value <> "") THEN
		IF Filtered THEN
			myQuery = myQuery + " and Title like '" + STitle.Value + "%'"
		ELSE
			myQuery = myQuery + " where Title like '" + STitle.Value + "%'"
			Filtered = True
		END IF
	END IF

	IF (SEmail.Value <> "") THEN
		IF Filtered THEN
			myQuery = myQuery + " and Email like '" + SEmail.Value + "%'"
		ELSE
			myQuery = myQuery + " where Email like '" + SEmail.Value + "%'"
			Filtered = True
		END IF
	END IF

	'Set the new query and then refresh the DC1 so that the new results are displayed.

	DC1.SQL = myQuery
	DC1.Refresh	
END SUB

'Navigation subroutines - based on currency changes to RDS.DataControl (DC1).

'Move to the first record in the bound recordset.
SUB First_OnClick
  	DC1.Recordset.MoveFirst
	SetPosition(DC1.Recordset.AbsolutePosition)
END SUB


'Move to the next record from the current position in the bound recordset.
SUB Next_OnClick
	DC1.Recordset.MoveNext
	
	If DC1.Recordset.EOF Then
		DC1.Recordset.MoveLast
		MsgBox "Cannot move beyond last record.",,"Address Book Sample"
	End If
	
	SetPosition(DC1.Recordset.AbsolutePosition)
END SUB


'Move to the previous record from the current position in the bound recordset.
SUB Prev_OnClick
	DC1.Recordset.MovePrevious
	
	If DC1.Recordset.BOF Then
		DC1.Recordset.MoveFirst
		MsgBox "Cannot move before first record.",,"Address Book Sample"
	End If

	SetPosition(DC1.Recordset.AbsolutePosition)
END SUB


'Move to the last record in the bound recordset.
SUB Last_OnClick
  	DC1.Recordset.MoveLast
	SetPosition(DC1.Recordset.AbsolutePosition)
END SUB


'Submit edits made and pull a clean copy of the new data.
SUB Update_OnClick
 	DC1.SubmitChanges
	DC1.Refresh        ' A Refresh is not required in RDS 2.0 after a SubmitChanges, but it ensures fresh data.
END SUB

'Cancel edits and restore original values.
SUB Cancel_OnClick
  	DC1.CancelUpdate
END SUB

</SCRIPT>

<BR>
<font color = "#400040">This site powered by the Microsoft Data Access Components. </font>
</BODY>
</HTML>
